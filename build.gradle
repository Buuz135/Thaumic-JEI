buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath 'com.selesse:gradle-git-changelog:0.2.+'
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.0.10"
    }
}
plugins {
    id "com.github.hierynomus.license" version "0.14.0"
    id "com.matthewprenger.cursegradle" version "1.0.10"
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.selesse.git.changelog'
apply plugin: 'java'


String b = ""
if (project.hasProperty("teamcity")) {
    b = System.getenv("BUILD_NUMBER")
}

if (b == null || b == "") {
    version = "${project.minecraftVersion}-${modVersion}"
} else {
    version = "${project.minecraftVersion}-${modVersion}-" + b
}

group = modGroup
archivesBaseName = modBaseName

sourceCompatibility = "1.8"
targetCompatibility = "1.8" // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}


minecraft {
    version = project.forgeVersion
    runDir = "run"

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = project.mcpVersion
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

repositories {
    mavenCentral()
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "modmaven.k-4u.nl"
    }
    maven {
        name 'Curseforge Maven' // Thaumcraft
        url 'http://minecraft.curseforge.com/api/maven/'
    }
}

dependencies {
    deobfCompile "mezz.jei:jei_1.12.2:${project.jei_version}:api"
    // at runtime, use the full JEI jar
    runtime "mezz.jei:jei_1.12.2:${project.jei_version}"
    deobfCompile "thaumcraft:Thaumcraft:${project.minecraftVersion}:${project.thaumcraft}"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

license {
    header rootProject.file('HEADER')
    include "**/*.java"
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.name = 'Buuz135'
    ignoreFailures = true
    mapping {
        java='SLASHSTAR_STYLE'
    }
}

changelog {
    title = "Thaumic JEI"
    fileName = "CHANGELOG.md"
    outputDirectory = file("$projectDir")
    since = '1.4.0'
    commitFormat = '%s (%an)'
    markdown {
        commitFormat = '* %s (%an)'
    }
    includeLines = {
        !it.contains("Merge")
    }
    processLines = {
        String input = it as String
        if (input.contains('[ci skip] ')) {
            input = input.minus('[ci skip] ')
        }
        input
    }
}

tasks.curseforge.enabled =  project.hasProperty('curse_api')

curseforge {
    if (project.hasProperty('curse_api')) {
        apiKey = project.curse_api
    }
    project {
        id = '285492'
        changelog = file('CHANGELOG.md')
        changelogType = 'markdown'
        releaseType = 'beta'
    }
}
